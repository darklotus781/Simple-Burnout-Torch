plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.96'
    id "me.shedaniel.unified-publishing" version "0.1.+"
}

def secrets = new Properties()
def secretsFile = file('secrets.properties')
if (secretsFile.exists()) {
    secretsFile.withInputStream {
        stream -> secrets.load(stream)
    }

    fileTree("secrets").matching {
        include "**/*.properties"
    }.each {
        File file ->
            file.withInputStream {
                stream -> secrets.load(stream)
            }
    }
}

version = mod_version
group = mod_group_id
var mrToken = secrets.getProperty('mr_token')
var mrID = "MqJnLmSS"
var cfToken = secrets.getProperty('cf_token')
var cfID = "1353923"

repositories {
    mavenLocal()
    maven {
        // location of the maven that hosts JEI files since January 2023
        // location of the maven for Vazkii's mods
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

base {
    archivesName = mod_id + "-neoforge-" + minecraft_version
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

// Include resources generated by data generators.
sourceSets {
    data {
        compileClasspath += main.compileClasspath + main.output

        java {
            srcDirs = ['src/main/java', 'src/data/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

neoForge {
    evaluationDependsOnChildren()

    // Specify the version of NeoForge to use.
    version = project.neo_version
    validateAccessTransformers = true

    addModdingDependenciesTo sourceSets.data

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // mostly optional in a single mod project
        // but multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }


    // This line is optional. Access Transformers are automatically detected
    // accessTransformers.add('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        configureEach {
            gameDirectory = project.file('run')
            systemProperty 'forge.logging.console.level', 'debug'
            logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()
            systemProperty 'forge.enabledGameTestNamespaces', "${project.mod_id}"
            programArguments.addAll '--username', secrets.getProperty("username") ?: 'Dev', secrets.getProperty("uuid") ? '--uuid' : '', secrets.getProperty("uuid") ?: ''
        }

        server {
            server()
            systemProperty 'forge.enabledGameTestNamespaces', "${project.mod_id}"
            programArgument '--nogui'
        }

        data {
            data()
            programArguments.addAll '--mod', "${project.mod_id}", '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
            sourceSet = this.sourceSets.data
        }
    }
}

dependencies {
    compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}"
    // We add the full version to localRuntime, not runtimeOnly, so that we do not publish a dependency on it
    implementation "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"
    implementation "curse.maven:just-enough-resources-jer-240630:6174588"
    implementation "curse.maven:jade-324717:6291517"
    implementation "curse.maven:cucumber-272335:6481512"
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [minecraft_version      : minecraft_version,
                             minecraft_version_range: minecraft_version_range,
                             neo_version            : neo_version,
                             neo_version_range      : neo_version_range,
                             loader_version_range   : loader_version_range,
                             mod_id                 : mod_id,
                             mod_name               : mod_name,
                             mod_license            : mod_license,
                             mod_version            : mod_version,
                             mod_authors            : mod_authors,
                             mod_description        : mod_description]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

// ─────────────────────────────────────────────────────────────
// Unified Publishing setup (Modrinth + CurseForge)
// ─────────────────────────────────────────────────────────────
unifiedPublishing {
    project {
        // Jar to upload — for NeoForge use the built remapped jar
        mainPublication tasks.jar
        // Shared metadata
        version = project.version
        changelog = rootProject.file("changelog/${minecraft_version}-${mod_version}.md").text
        releaseType = "release"
        gameVersions = [minecraft_version]
        gameLoaders = ["neoforge"]

        // CurseForge configuration
        if (cfToken != null && !cfToken.isBlank()) {
            curseforge {
                displayName = "${mod_name} - ${minecraft_version} - ${project.version}"
                token = cfToken
                id = cfID
            }
        }

        // Modrinth configuration
        if (mrToken != null && !mrToken.isBlank()) {
            modrinth {
                displayName = "${mod_name} ${project.version}"
                token = mrToken
                id = mrID
            }
        }
    }
}

